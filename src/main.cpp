#include <Arduino.h>
#include <TFT_eSPI.h>
#include <WiFi.h>
#include <WebSocketsClient.h>
#include <ArduinoJson.h>

//colors inverted in a weird way (screen software issue had to test alot of colors)
#define MY_BLACK TFT_WHITE  
#define MY_WHITE TFT_BLACK
#define MY_GREEN 0xF81F
#define MY_RED   0xFFE0
#define MY_GRAY  0x7BEF

const char* ssid = "ur wifi name";
const char* password = "ur wifi password";
const char* ws_host = "ur WS link";

TFT_eSPI tft = TFT_eSPI();
WebSocketsClient webSocket;

// NVIDIA Logo 
const unsigned char nvidia_logo[] PROGMEM = {
  0xff, 0xe0, 0x00, 0x00, 0xff, 0xe0, 0x00, 0x00, 0xff, 0xe0, 0x00, 0x00, 0xff, 0xc0, 0x00, 0x00,
  0xfe, 0x00, 0x00, 0x00, 0xfc, 0x01, 0x00, 0x00, 0xf8, 0x00, 0xc0, 0x00, 0xf0, 0x00, 0x20, 0x00,
  0xe0, 0xe0, 0x18, 0x00, 0xc1, 0x80, 0x08, 0x00, 0x83, 0x04, 0x0c, 0x00, 0x02, 0x07, 0x06, 0x00,
  0x04, 0x01, 0x82, 0x00, 0x08, 0x21, 0x82, 0x00, 0x08, 0x61, 0x06, 0x00, 0x08, 0x60, 0x04, 0x00,
  0x04, 0x60, 0x08, 0x00, 0x04, 0x20, 0x08, 0x00, 0x80, 0x00, 0x00, 0x00, 0x82, 0x00, 0x20, 0x60,
  0xc1, 0x00, 0x40, 0xf0, 0xc1, 0x00, 0x01, 0xe0, 0xe0, 0x84, 0x03, 0xe0, 0xf0, 0x40, 0x03, 0x80,
  0xf0, 0x20, 0x0e, 0x00, 0xf8, 0x00, 0x38, 0x00, 0xfc, 0x00, 0xe0, 0x00, 0xfe, 0x06, 0x00, 0x00,
  0xff, 0x80, 0x00, 0x00, 0xff, 0xe0, 0x00, 0x00, 0xff, 0xe0, 0x00, 0x00, 0xff, 0xe0, 0x00, 0x00
};

// Palantir Logo 
const unsigned char palantir_logo[] PROGMEM = {
  0xff, 0x80, 0x01, 0xff, 0xfe, 0x00, 0x00, 0x7f, 0xf8, 0x00, 0x00, 0x1f, 0xf0, 0x00, 0x00, 0x0f,
  0xe0, 0x01, 0x80, 0x07, 0xc0, 0x1f, 0xf8, 0x03, 0x80, 0x7f, 0xfc, 0x01, 0x00, 0xff, 0xff, 0x00,
  0x01, 0xff, 0xff, 0x80, 0x01, 0xff, 0xff, 0x80, 0x03, 0xff, 0xff, 0xc0, 0x03, 0xff, 0xff, 0xc0,
  0x03, 0xff, 0xff, 0xc0, 0x03, 0xff, 0xff, 0xc0, 0x01, 0xff, 0xff, 0x80, 0x01, 0xff, 0xff, 0x80,
  0x00, 0xff, 0xff, 0x00, 0x80, 0x7f, 0xfe, 0x01, 0xc0, 0x3f, 0xfc, 0x03, 0xc0, 0x07, 0xe0, 0x03,
  0xf0, 0x00, 0x00, 0x0f, 0xf0, 0x00, 0x00, 0x0f, 0xfc, 0x00, 0x00, 0x3f, 0x8f, 0x00, 0x00, 0xf1,
  0x00, 0xf0, 0x0f, 0x00, 0x00, 0x1f, 0xf8, 0x00, 0x00, 0x07, 0xe0, 0x00, 0x00, 0x00, 0x00, 0x00,
  0xc0, 0x00, 0x00, 0x03, 0xfc, 0x00, 0x00, 0x3f, 0xff, 0x80, 0x01, 0xff, 0xff, 0xf0, 0x0f, 0xff
};

// Meta Logo 
const unsigned char meta_logo[] PROGMEM = {
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x01, 0xf0, 0x07, 0x00, 0x07, 0xfc, 0x1f, 0xc0,
  0x0f, 0xfe, 0x38, 0xe0, 0x0f, 0x1f, 0x70, 0x30, 0x1c, 0x07, 0xe0, 0x38, 0x1c, 0x03, 0xc0, 0x18,
  0x38, 0x03, 0xc0, 0x0c, 0x38, 0x03, 0xc0, 0x0c, 0x70, 0x03, 0xe0, 0x0e, 0x70, 0x07, 0xf0, 0x0e,
  0x70, 0x0e, 0x70, 0x0e, 0x70, 0x0c, 0x78, 0x06, 0x70, 0x1c, 0x3c, 0x06, 0x70, 0x18, 0x1c, 0x06,
  0x70, 0x38, 0x1e, 0x06, 0x70, 0x70, 0x0f, 0x0e, 0x78, 0xe0, 0x07, 0x0e, 0x3f, 0xe0, 0x07, 0xfc,
  0x1f, 0xc0, 0x03, 0xf8, 0x0f, 0x00, 0x00, 0xf0, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};

// Bitcoin Logo
const unsigned char bitcoin_logo[] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0x0f, 0xff,
  0xff, 0xc0, 0x03, 0xff, 0xff, 0x0f, 0xf0, 0xff, 0xfe, 0x3f, 0xfc, 0x7f, 0xfc, 0xf9, 0x3f, 0x3f,
  0xf9, 0xf9, 0x3f, 0x9f, 0xf9, 0xf9, 0x3f, 0x9f, 0xf3, 0xc0, 0x0f, 0xcf, 0xf3, 0xc0, 0x07, 0xcf,
  0xe7, 0xf1, 0xc3, 0xe7, 0xe7, 0xf1, 0xe3, 0xe7, 0xe7, 0xf1, 0xc7, 0xe7, 0xe7, 0xf0, 0x07, 0xf7,
  0xe7, 0xf0, 0x03, 0xf7, 0xe7, 0xf1, 0xe3, 0xe7, 0xe7, 0xf1, 0xe3, 0xe7, 0xe7, 0xf1, 0xe3, 0xe7,
  0xf3, 0xc0, 0x03, 0xcf, 0xf3, 0xc0, 0x07, 0xcf, 0xf9, 0xf9, 0x3f, 0x9f, 0xf9, 0xf9, 0x3f, 0x9f,
  0xfc, 0xf9, 0x3f, 0x3f, 0xfe, 0x3f, 0xfc, 0x7f, 0xff, 0x0f, 0xf0, 0xff, 0xff, 0xc1, 0x83, 0xff,
  0xff, 0xf0, 0x0f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff
};

// Ethereum Logo 
const unsigned char ethereum_logo[] PROGMEM = {
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x01, 0x80, 0x00, 0x00, 0x01, 0x80, 0x00,
  0x00, 0x03, 0xc0, 0x00, 0x00, 0x03, 0xe0, 0x00, 0x00, 0x07, 0xe0, 0x00, 0x00, 0x0f, 0xf0, 0x00,
  0x00, 0x0f, 0xf0, 0x00, 0x00, 0x1f, 0xf8, 0x00, 0x00, 0x1f, 0xfc, 0x00, 0x00, 0x3f, 0xfc, 0x00,
  0x00, 0x7f, 0xfe, 0x00, 0x00, 0x7f, 0xfe, 0x00, 0x00, 0xff, 0xff, 0x00, 0x00, 0xff, 0xff, 0x80,
  0x01, 0xff, 0xff, 0x80, 0x00, 0x7f, 0xfe, 0x00, 0x00, 0x3f, 0xfc, 0x00, 0x00, 0xcf, 0xf3, 0x00,
  0x00, 0x63, 0xc6, 0x00, 0x00, 0x38, 0x1e, 0x00, 0x00, 0x3e, 0x7c, 0x00, 0x00, 0x1f, 0xf8, 0x00,
  0x00, 0x0f, 0xf8, 0x00, 0x00, 0x0f, 0xf0, 0x00, 0x00, 0x07, 0xe0, 0x00, 0x00, 0x03, 0xc0, 0x00,
  0x00, 0x01, 0xc0, 0x00, 0x00, 0x01, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};

// Solana Logo
const unsigned char solana_logo[] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xc0, 0x00, 0x7f, 0xff, 0x80, 0x00, 0xff, 0xff, 0x00, 0x00, 0xff, 0xfe, 0x00, 0x01, 0xff,
  0xff, 0x00, 0x07, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x00, 0x03, 0xff, 0xff, 0x00, 0x01, 0xff,
  0xff, 0x80, 0x00, 0xff, 0xff, 0xc0, 0x00, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xe0, 0x00, 0xff,
  0xff, 0x80, 0x00, 0x7f, 0xff, 0x80, 0x00, 0xff, 0xff, 0x00, 0x01, 0xff, 0xff, 0x00, 0x03, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff
};

// Draw bitmap helper
void drawBitmap(int x, int y, const unsigned char* bitmap, int w, int h, uint16_t color) {
    for(int j = 0; j < h; j++) {
        for(int i = 0; i < w; i++) {
            int byteIndex = (j * ((w + 7) / 8)) + (i / 8);
            int bitIndex = 7 - (i % 8);
            if(pgm_read_byte(&bitmap[byteIndex]) & (1 << bitIndex)) {
                tft.drawPixel(x + i, y + j, color);
            }
        }
    }
}


void updatePriceDisplay(int x, int y, String label, String price, bool isUp) {
    uint16_t color = isUp ? MY_GREEN : MY_RED;
    
    // Clear text area
    tft.fillRect(x + 40, y + 5, 190, 50, MY_BLACK); 

    // Label
    tft.setTextSize(1);
    tft.setTextColor(MY_GRAY);
    tft.setCursor(x + 42, y + 8);
    tft.print(label);

    // Price
    tft.setTextColor(color);
    tft.setTextSize(3);
    tft.setCursor(x + 42, y + 22);
    tft.print("$" + price);
    
    // Arrow
    int arrowX = x + 180;
    int arrowY = y + 28;
    if(isUp) {
        tft.fillTriangle(arrowX, arrowY+8, arrowX+10, arrowY+8, arrowX+5, arrowY, MY_GREEN);
    } else {
        tft.fillTriangle(arrowX, arrowY, arrowX+10, arrowY, arrowX+5, arrowY+8, MY_RED);
    }
}

void drawStaticUI() {
    tft.fillScreen(MY_BLACK);
    
    // Divider
    tft.fillRect(238, 0, 4, 320, MY_GRAY);
    
    // Header
    tft.fillRect(0, 0, 480, 35, MY_GRAY);
    
    // Titles
    tft.setTextSize(2);
    tft.setTextColor(MY_BLACK);
    tft.setCursor(15, 10);  
    tft.print("STOCKS");
    tft.setCursor(255, 10); 
    tft.print("CRYPTO");

    // Stock logos
    drawBitmap(4, 48, nvidia_logo, 32, 32, MY_GREEN);      // NVIDIA
    drawBitmap(4, 133, palantir_logo, 32, 32, MY_WHITE);    // Palantir
    drawBitmap(4, 218, meta_logo, 32, 32, 0x841F);        // Meta
    
    // Crypto logos
    drawBitmap(244, 48, bitcoin_logo, 32, 32, 0xFC1F);      // Bitcoin
    drawBitmap(244, 133, ethereum_logo, 32, 32, 0x8410);     // Ethereum
    drawBitmap(244, 218, solana_logo, 32, 32, 0x9C1F);      // Solana
    
    // Separators
    tft.drawFastHLine(5, 120, 230, MY_GRAY);
    tft.drawFastHLine(5, 205, 230, MY_GRAY);
    tft.drawFastHLine(245, 120, 230, MY_GRAY);
    tft.drawFastHLine(245, 205, 230, MY_GRAY);
}

void webSocketEvent(WStype_t type, uint8_t * payload, size_t length) {
    switch(type) {
        case WStype_CONNECTED:
            Serial.println("[WS] Connected");
            webSocket.sendTXT("{\"type\":\"subscribe\",\"payload\":{\"channel\":\"stock\",\"symbols\":[\"NVDA\",\"PLTR\",\"META\"]}}");
            webSocket.sendTXT("{\"type\":\"subscribe\",\"payload\":{\"channel\":\"crypto\",\"symbols\":[\"bitcoin\",\"ethereum\",\"solana\"]}}");
            break;

        case WStype_TEXT: {
            JsonDocument doc;
            deserializeJson(doc, payload);
            
            if (doc["type"] == "update") {
                String channel = doc["channel"];
                String sym = doc["symbol"];
                
                if (channel == "stock") {
                    float p = doc["data"]["c"].as<float>();
                    bool up = doc["data"]["dp"].as<float>() >= 0;
                    
                    if (sym == "NVDA") updatePriceDisplay(5, 50, "NVIDIA", String(p, 2), up);
                    if (sym == "PLTR") updatePriceDisplay(5, 135, "PALANTIR", String(p, 2), up);
                    if (sym == "META") updatePriceDisplay(5, 220, "META", String(p, 2), up);
                } 
                else if (channel == "crypto") {
                    float p = doc["data"]["price"].as<float>();
                    bool up = true; 

                    if (sym == "bitcoin")  updatePriceDisplay(245, 50, "BITCOIN", String(p, 0), up);
                    if (sym == "ethereum") updatePriceDisplay(245, 135, "ETHEREUM", String(p, 2), up);
                    if (sym == "solana")   updatePriceDisplay(245, 220, "SOLANA", String(p, 2), up);
                }
            }
            break;
        }
        case WStype_DISCONNECTED:
            Serial.println("[WS] Lost Connection");
            break;
    }
}

void setup() {
    Serial.begin(115200);
    tft.init();
    tft.setRotation(1);
    drawStaticUI();

    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) { delay(500); }

    webSocket.beginSSL(ws_host, 443, "/");
    webSocket.onEvent(webSocketEvent);
    webSocket.setReconnectInterval(5000);
}

void loop() {
    webSocket.loop();
}
